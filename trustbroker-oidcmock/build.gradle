plugins {
	id 'application'
	id "org.springframework.boot"
	id 'com.google.cloud.tools.jib'
}

dependencies {
	implementation "org.springframework.boot:spring-boot-starter-web"
	implementation "org.springframework.boot:spring-boot-starter-security"
	implementation "org.springframework.security:spring-security-oauth2-authorization-server"
}

springBoot {
	mainClassName = 'swiss.trustbroker.oidcmock.OidcMockApplication'
}

// JIB publshing
jib {
	from {
		image = rootProject.baseImage
	}
	to {
		image = rootProject.dockerRegistry + '/trustbroker-swiss/trustbroker-oidcmock'
		tags += project.version
		auth {
			username = project.repoUser
			password = project.repoPass
		}
	}
	container {
		user = "nobody:nobody" // uid=99, gid=99
		creationTime = "USE_CURRENT_TIMESTAMP"
	}
}

publishing {
	publications {
		uberJar(MavenPublication) {
			artifact bootJar
		}
	}
}

artifactoryPublish {
	publications(publishing.publications.uberJar)
}

tasks.jibDockerBuild.dependsOn assemble
tasks.jib.dependsOn assemble
